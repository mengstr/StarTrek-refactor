270 Z$="                         "

290 REM Device indices
291 WARPENGINES = 1    : REM Warp engines device index
292 SRSSENSOR = 2      : REM Short range sensor device index
293 LRSSENSOR = 3      : REM Long range sensor device index
294 PHASERS = 4        : REM Phaser control device index
295 PHOTONTORPEDO = 5  : REM Photon torpedo device index
296 DAMAGECONTROL = 6  : REM Damage control device index
297 SHIELDS = 7        : REM Shield control device index
298 COMPUTER = 8       : REM Library-computer device index

300 KLINGX = 1     : REM Constant for KLINGONS array index for X coordinate
301 KLINGY = 2     : REM Constant for KLINGONS array index for Y coordinate  
302 KLINGSTR = 3   : REM Constant for KLINGONS array index for strength

330 DIM GALAXY(8,8)
331 DIM C(9,2)
332 DIM KLINGONS(3,3)
333 DIM N(3)
334 DIM SEEN(8,8)
335 DIM DEVICE(8)
370 INITDATE=INT(RND(1)*20+20)*100
371 DATE=INITDATE
372 TIMELIMIT=25+INT(RND(1)*10)
373 ISDOCKED=0
374 INITENERGY=3000
375 ENERGY=INITENERGY
440 INITTORP=10
441 TORPEDOES=INITTORP
442 MAXKLINGONSTR=200
443 SHIELD=0
444 TOTALBASES=0
445 TOTALKLINGONS=0
446 X$=""
447 X0$=" IS "
470 DEF FND(D)=SQR((KLINGONS(I,KLINGX)-SECTX)^2+(KLINGONS(I,KLINGY)-SECTY)^2)

490 QUADX=INT(RND(1)*8+1)
491 QUADY=INT(RND(1)*8+1)
492 SECTX=INT(RND(1)*8+1)
493 SECTY=INT(RND(1)*8+1)

530 FOR I=1 TO 9
531     C(I,1)=0
532     C(I,2)=0
533 NEXT I
540 C(3,1)=-1
541 C(2,1)=-1
542 C(4,1)=-1
543 C(4,2)=-1
544 C(5,2)=-1
545 C(6,2)=-1
600 C(1,2)=1
601 C(2,2)=1
602 C(6,1)=1
603 C(7,1)=1
604 C(8,1)=1
605 C(8,2)=1
606 C(9,2)=1

670 FOR I=1 TO 8
671     DEVICE(I)=0
672 NEXT I

710 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"

820 FOR I=1 TO 8
821    FOR J=1 TO 8
822       KLINGONCNT=0
823       SEEN(I,J)=0
824       R1=RND(1)
850       IF R1>.8 THEN KLINGONCNT=1
860       IF R1>.95 THEN KLINGONCNT=2
870       IF R1>.98 THEN KLINGONCNT=3
880       TOTALKLINGONS=TOTALKLINGONS+KLINGONCNT
980       BASECNT=0
981       IF RND(1)>.96 THEN BASECNT=1
982       TOTALBASES=TOTALBASES+BASECNT
1040      GALAXY(I,J)=KLINGONCNT*100+BASECNT*10+INT(RND(1)*8+1)
1041   NEXT J
1042 NEXT I

1043 IF TOTALKLINGONS>TIMELIMIT THEN TIMELIMIT=TOTALKLINGONS+1
1100 IF TOTALBASES<>0 THEN GOTO 1200
1150 IF GALAXY(QUADX,QUADY)<200 THEN GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)+100:TOTALKLINGONS=TOTALKLINGONS+1
1160 TOTALBASES=1
1161 GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)+10
1162 QUADX=INT(RND(1)*8+1)
1163 QUADY=INT(RND(1)*8+1)

1200 INITKLINGONS=TOTALKLINGONS
1201 IF TOTALBASES<>1 THEN X$="S":X0$=" ARE "
1230 PRINT"YOUR ORDERS ARE AS FOLLOWS:"
1235 PRINT "--------------------------"
1240 PRINT"   DESTROY THE";TOTALKLINGONS;"KLINGON WARSHIPS WHICH HAVE INVADED"
1250 PRINT"   THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
1260 PRINT"   ON STARDATE";INITDATE+TIMELIMIT;". THIS GIVES YOU";TIMELIMIT;"DAYS. THERE";X0$
1270 PRINT"  ";TOTALBASES;"STARBASE";X$;" IN THE GALAXY FOR RESUPPLYING YOUR SHIP."
1280 PRINT
1310 REM
1320 Z4=QUADX
1321 Z5=QUADY
1322 KLINGONCNT=0
1323 BASECNT=0
1324 STARCNT=0
1325 BASENAMEONLY=0
1326 REPAIRFACTOR=.5*RND(1)
1327 SEEN(QUADX,QUADY)=GALAXY(QUADX,QUADY)
1390 IF QUADX<1 OR QUADX>8 OR QUADY<1 OR QUADY>8 THEN GOTO 1600
1430 GOSUB 9030 : REM Get region name : REM Get star system name
1431 PRINT
1432 IF INITDATE<>DATE THEN GOTO 1490
1460 PRINT"YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
1470 PRINT"IN THE GALACTIC QUADRANT, '";G2$;"'."
1471 GOTO 1500
1490 PRINT"NOW ENTERING ";G2$;" QUADRANT . . ."
1500 PRINT
1501 KLINGONCNT=INT(GALAXY(QUADX,QUADY)*.01)
1502 BASECNT=INT(GALAXY(QUADX,QUADY)*.1)-10*KLINGONCNT
1540 STARCNT=GALAXY(QUADX,QUADY)-100*KLINGONCNT-10*BASECNT
1541 IF KLINGONCNT=0 THEN GOTO 1590
1560 PRINT TAB(3);"  COMBAT AREA      CONDITION RED  "
1561 IF SHIELD>200 THEN PRINT:GOTO 1590
1580 PRINT TAB(3);"      SHIELDS DANGEROUSLY LOW     "
1581 PRINT
1590 FOR I=1 TO 3
1591     KLINGONS(I,KLINGX)=0
1592     KLINGONS(I,KLINGY)=0
1593 NEXT I
1600 FOR I=1 TO 3
1601     KLINGONS(I,KLINGSTR)=0
1602 NEXT I
1603 Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
1680 A$="<E>"
1681 Z1=SECTX
1682 Z2=SECTY
1683 GOSUB 8670 : REM Place object in map
1684 IF KLINGONCNT<1 THEN GOTO 1820

1720 FOR I=1 TO KLINGONCNT
1721     GOSUB 8590 : REM Get empty map location
1722     A$="+K+"
1723     Z1=R1
1724     Z2=R2
1780     GOSUB 8670 : REM Place object in map
1781     KLINGONS(I,KLINGX)=R1
1782     KLINGONS(I,KLINGY)=R2
1783     KLINGONS(I,KLINGSTR)=MAXKLINGONSTR*(.5+RND(1))
1784 NEXT I

1820 IF BASECNT<1 THEN GOTO 1910
1880 GOSUB 8590 : REM Get empty map location
1881 A$=">B<"
1882 Z1=R1
1883 BASEX=R1
1884 Z2=R2
1885 BASEY=R2
1886 GOSUB 8670 : REM Place object in map

1910 FOR I=1 TO STARCNT
1911     GOSUB 8590 : REM Get empty map location
1912     A$=" * "
1913     Z1=R1
1914     Z2=R2
1915     GOSUB 8670 : REM Place object in map
1916 NEXT I

1980 GOSUB 6430 : REM Check for nearby starbases

1987 REM ----------------------------------------
1988 REM  SRS - SHORT RANGE SENSOR SCAN
1989 REM ----------------------------------------

1990 IF SHIELD+ENERGY>10 THEN IF ENERGY>10 OR DEVICE(SHIELDS)=0 THEN GOTO 2060
2020 PRINT
2021 PRINT TAB(10);"** FATAL ERROR **"
2022 PRINT"YOU'VE JUST STRANDED YOUR SHIP IN SPACE."
2030 PRINT"YOU HAVE INSUFFICIENT MANEUVERING ENERGY,"
2040 PRINT"AND SHIELD CONTROL IS PRESENTLY INCAPABLE OF"
2050 PRINT"CROSS-CIRCUITING TO ENGINE ROOM!!"
2051 PRINT
2052 GOTO 6220

2057 REM ----------------------------------------
2058 REM  MAIN COMMANDS
2059 REM ----------------------------------------

2060 PRINT
2061 INPUT"COMMAND";A$
2062 PRINT
2080 FOR I=1 TO 9
2081     IF LEFT$(A$,3)<>MID$(A1$,3*I-2,3)THEN GOTO 2160
2140     IF I=1 THEN GOTO 2300
2141     IF I=2 THEN GOTO 1980
2142     IF I=3 THEN GOTO 4000
2143     IF I=4 THEN GOTO 4260
2144     IF I=5 THEN GOTO 4700
2145     IF I=6 THEN GOTO 5530
2146     IF I=7 THEN GOTO 5690
2147     IF I=8 THEN GOTO 7290
2148     IF I=9 THEN GOTO 6270
2160 NEXT I
2161 PRINT"ENTER ONE OF THE FOLLOWING:"
2170 PRINT "--------------------------"
2180 PRINT"  NAV  (TO SET COURSE)"
2190 PRINT"  SRS  (FOR SHORT RANGE SENSOR SCAN)"
2200 PRINT"  LRS  (FOR LONG RANGE SENSOR SCAN)"
2210 PRINT"  PHA  (TO FIRE PHASERS)"
2220 PRINT"  TOR  (TO FIRE PHOTON TORPEDOES)"
2230 PRINT"  SHE  (TO RAISE OR LOWER SHIELDS)"
2240 PRINT"  DAM  (FOR DAMAGE CONTROL REPORTS)"
2250 PRINT"  COM  (TO CALL ON LIBRARY-COMPUTER)"
2260 PRINT"  XXX  (TO RESIGN YOUR COMMAND)":PRINT
2261 GOTO 1990 : REM Main Loop

2257 REM ----------------------------------------
2258 REM  NAV - NAVIGATION COMMAND
2259 REM ----------------------------------------

2300 INPUT"COURSE (0-9)";C1
2301 IF C1=9 THEN C1=1
2310 IF C1>=1 AND C1<9 THEN GOTO 2350
2330 PRINT"   LT. SULU: 'INCORRECT COURSE DATA, SIR!'"
2331 GOTO 1990 : REM Main Loop
2350 X$="8"
2351 IF DEVICE(WARPENGINES)<0 THEN X$="0.2"
2360 PRINT"WARP FACTOR (0-";X$;")";
2361 INPUT W1
2362 PRINT
2363 IF DEVICE(WARPENGINES)<0 AND W1>.2 THEN GOTO 2470
2380 IF W1>0 AND W1<=8 THEN GOTO 2490
2390 IF W1=0 THEN GOTO 1990 : REM Main Loop
2420 PRINT"   CHIEF ENGINEER SCOTT: 'THE ENGINES WON'T TAKE";
2430 PRINT" WARP";W1;"!'"
2431 GOTO 1990 : REM Main Loop
2470 PRINT"WARP ENGINES ARE DAMAGED.  MAXIUM SPEED = WARP 0.2"
2471 GOTO 1990 : REM Main Loop
2490 N=INT(W1*8+.5)
2491 IF ENERGY-N>=0 THEN GOTO 2590
2500 PRINT"ENGINEERING: 'INSUFFICIENT ENERGY AVAILABLE"
2510 PRINT"               FOR MANEUVERING AT WARP";W1;"!'"
2530 IF SHIELD<N-ENERGY OR DEVICE(SHIELDS)<0 THEN GOTO 1990 : REM Main Loop
2550 PRINT"DEFLECTOR CONTROL ROOM:  ";SHIELD;"UNITS OF ENERGY"
2560 PRINT"                          PRESENTLY DEPLOYED TO SHIELDS."
2570 GOTO 1990 : REM Main Loop

2590 FOR I=1 TO KLINGONCNT
2591     IF KLINGONS(I,KLINGSTR)=0 THEN GOTO 2700
2610     A$="   "
2611     Z1=KLINGONS(I,KLINGX)
2612     Z2=KLINGONS(I,KLINGY)
2613     GOSUB 8670 : REM Place object in map
2614     GOSUB 8590 : REM Get empty map location
2660     KLINGONS(I,KLINGX)=Z1
2661     KLINGONS(I,KLINGY)=Z2
2662     A$="+K+"
2663     GOSUB 8670 : REM Place object in map
2700 NEXT I
2701 GOSUB 6000 : REM Klingon attack
2702 D1=0
2703 D6=W1
2704 IF W1>=1 THEN D6=1

2770 FOR I=1 TO 8
2771     IF DEVICE(I)>=0 THEN GOTO 2880
2790     DEVICE(I)=DEVICE(I)+D6
2791     IF DEVICE(I)>-.1 AND DEVICE(I)<0 THEN DEVICE(I)=-.1:GOTO 2880
2800     IF DEVICE(I)<0 THEN GOTO 2880
2810     IF D1<>1 THEN D1=1:PRINT"DAMAGE CONTROL REPORT:  ";
2840     PRINT TAB(8);
2841     R1=I
2842     GOSUB 8790 : REM Get device name
2843     PRINT G2$;" REPAIR COMPLETED."
2880 NEXT I

2881 IF RND(1)>.2 THEN GOTO 3070
2910 R1=INT(RND(1)*8+1)
2911 IF RND(1)>=.6 THEN GOTO 3000
2930 DEVICE(R1)=DEVICE(R1)-(RND(1)*5+1)
2931 PRINT"DAMAGE CONTROL REPORT: ";
2960 GOSUB 8790 : REM Get device name
2961 PRINT G2$;" DAMAGED"
2962 PRINT
2963 GOTO 3070
3000 DEVICE(R1)=DEVICE(R1)+RND(1)*3+1
3001 PRINT"DAMAGE CONTROL REPORT:  ";
3030 GOSUB 8790 : REM Get device name
3031 PRINT G2$;" STATE OF REPAIR IMPROVED"
3032 PRINT
3070 A$="   "
3071 Z1=INT(SECTX)
3072 Z2=INT(SECTY)
3073 GOSUB 8670 : REM Place object in map
3110 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1))
3111 X=SECTX
3112 Y=SECTY
3140 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1))
3141 Q4=QUADX
3142 Q5=QUADY

3170 FOR I=1 TO N
3171     SECTX=SECTX+X1
3172     SECTY=SECTY+X2
3173     IF SECTX<1 OR SECTX>=9 OR SECTY<1 OR SECTY>=9 THEN GOTO 3500
3240     S8=INT(SECTX)*24+INT(SECTY)*3-26
3241     IF MID$(Q$,S8,2)="  "THEN GOTO 3360
3320     SECTX=INT(SECTX-X1)
3321     SECTY=INT(SECTY-X2)
3322     PRINT"WARP ENGINES SHUT DOWN AT ";
3350     PRINT"SECTOR";SECTX;;",";SECTY;"DUE TO BAD NAVAGATION"
3351     GOTO 3370
3360 NEXT I

3361 SECTX=INT(SECTX)
3362 SECTY=INT(SECTY)
3370 A$="<E>"
3371 Z1=INT(SECTX)
3372 Z2=INT(SECTY)
3373 GOSUB 8670 : REM Place object in map
3374 GOSUB 3910 : REM Energy consumption calc
3375 T8=1
3430 IF W1<1 THEN T8=.1*INT(10*W1)
3450 DATE=DATE+T8
3451 IF DATE>INITDATE+TIMELIMIT THEN GOTO 6220
3480 GOTO 1980
3500 X=8*QUADX+X+N*X1
3501 Y=8*QUADY+Y+N*X2
3502 QUADX=INT(X/8)
3503 QUADY=INT(Y/8)
3504 SECTX=INT(X-QUADX*8)
3550 SECTY=INT(Y-QUADY*8)
3551 IF SECTX=0 THEN QUADX=QUADX-1:SECTX=8
3590 IF SECTY=0 THEN QUADY=QUADY-1:SECTY=8
3620 X5=0
3621 IF QUADX<1 THEN X5=1:QUADX=1:SECTX=1
3670 IF QUADX>8 THEN X5=1:QUADX=8:SECTX=8
3710 IF QUADY<1 THEN X5=1:QUADY=1:SECTY=1
3750 IF QUADY>8 THEN X5=1:QUADY=8:SECTY=8
3790 IF X5=0 THEN GOTO 3860
3800 PRINT"LT. UHURA: MESSAGE FROM STARFLEET COMMAND --"
3810 PRINT"  'PERMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMETER"
3820 PRINT"  IS HEREBY *DENIED*.  SHUT DOWN YOUR ENGINES.'"
3830 PRINT"CHIEF ENGINEER SCOTT:  'WARP ENGINES SHUT DOWN"
3840 PRINT"  AT SECTOR";SECTX;",";SECTY;"OF QUADRANT";QUADX;",";QUADY;".'"
3850 IF DATE>INITDATE+TIMELIMIT THEN GOTO 6220
3860 IF 8*QUADX+QUADY=8*Q4+Q5 THEN GOTO 3370
3870 DATE=DATE+1
3871 GOSUB 3910 : REM Energy consumption calc
3872 GOTO 1320

3909 REM Energy consumption calculations
3910 ENERGY=ENERGY-N-10
3911 IF ENERGY>=0 THEN RETURN
3930 PRINT"SHIELD CONTROL SUPPLIES ENERGY TO COMPLETE THE MANEUVER."
3940 SHIELD=SHIELD+ENERGY
3941 ENERGY=0
3942 IF SHIELD<=0 THEN SHIELD=0
3980 RETURN

3997 REM ----------------------------------------
3998 REM  LRS - LONG RANGE SENSOR SCAN
3999 REM ----------------------------------------

4000 IF DEVICE(LRSSENSOR)<0 THEN PRINT"LONG RANGE SENSORS ARE INOPERABLE.":GOTO 1990 : REM Main Loop
4030 PRINT"LONG RANGE SCAN FOR QUADRANT";QUADX;",";QUADY
4031 PRINT
4040 O1$="-------------------"
4041 PRINT O1$

4060 FOR I=QUADX-1 TO QUADX+1
4061     N(1)=-1
4062     N(2)=-2
4063     N(3)=-3
4064     FOR J=QUADY-1 TO QUADY+1
4120         IF I>0 AND I<9 AND J>0 AND J<9 THEN N(J-QUADY+2)=GALAXY(I,J):SEEN(I,J)=GALAXY(I,J)
4180     NEXT J
4181     FOR L=1 TO 3
4182         PRINT"| ";
4183         IF N(L)<0 THEN PRINT"*** ";:GOTO 4230
4210         PRINT RIGHT$(STR$(N(L)+1000),3);" ";
4230     NEXT L
4231     PRINT"|"
4232     PRINT O1$
4233 NEXT I
4234 GOTO 1990 : REM Main Loop

4257 REM ----------------------------------------
4258 REM  PHA - FIRE PHASERS
4259 REM ----------------------------------------

4260 IF DEVICE(PHASERS)<0 THEN PRINT"PHASERS INOPERATIVE.":GOTO 1990 : REM Main Loop
4265 IF KLINGONCNT>0 THEN GOTO 4330
4270 PRINT"SCIENCE OFFICER SPOCK:  'SENSORS SHOW NO ENEMY SHIPS"
4280 PRINT"                         IN THIS QUADRANT'"
4281 GOTO 1990 : REM Main Loop
4330 IF DEVICE(COMPUTER)<0 THEN PRINT"COMPUTER FAILURE HAMPERS ACCURACY."
4350 PRINT"PHASERS LOCKED ON TARGET;  "
4360 PRINT"ENERGY AVAILABLE =";ENERGY;"UNITS"
4370 INPUT"NUMBER OF UNITS TO FIRE";X
4371 IF X<=0 THEN GOTO 1990 : REM Main Loop
4400 IF ENERGY-X<0 THEN GOTO 4360
4410 ENERGY=ENERGY-X
4411 IF DEVICE(SHIELDS)<0 THEN X=X*RND(1)
4450 H1=INT(X/KLINGONCNT)

4451 FOR I=1 TO 3
4452     IF KLINGONS(I,KLINGSTR)<=0 THEN GOTO 4670
4480     H=INT((H1/FND(0))*(RND(1)+2))
4481     IF H>.15*KLINGONS(I,KLINGSTR)THEN GOTO 4530
4500     PRINT"SENSORS SHOW NO DAMAGE TO ENEMY AT";KLINGONS(I,KLINGX);",";KLINGONS(I,KLINGY);"."
4501     GOTO 4670
4530     KLINGONS(I,KLINGSTR)=KLINGONS(I,KLINGSTR)-H
4531     PRINT H;"UNIT HIT ON KLINGON AT SECTOR";KLINGONS(I,KLINGX);",";
4550     PRINT KLINGONS(I,KLINGY);"."
4551     IF KLINGONS(I,KLINGSTR)<=0 THEN PRINT:PRINT "*** KLINGON DESTROYED ***":PRINT:GOTO 4580
4560     PRINT" (SENSORS SHOW";KLINGONS(I,KLINGSTR);"UNITS REMAINING)"
4580     KLINGONCNT=KLINGONCNT-1
4581     TOTALKLINGONS=TOTALKLINGONS-1
4582     Z1=KLINGONS(I,KLINGX)
4583     Z2=KLINGONS(I,KLINGY)
4584     A$="   "
4585     GOSUB 8670 : REM Place object in map
4650     KLINGONS(I,KLINGSTR)=0
4651     GALAXY(QUADX,QUADY)=GALAXY(QUADX,QUADY)-100
4652     SEEN(QUADX,QUADY)=GALAXY(QUADX,QUADY)
4653     IF TOTALKLINGONS<=0 THEN GOTO 6370
4670 NEXT I

4671 GOSUB 6000 : REM Klingon attack
4672 GOTO 1990 : REM Main Loop

4697 REM ----------------------------------------
4698 REM  TOR - FIRE PHOTON TORPEDOES
4699 REM ----------------------------------------

4700 IF TORPEDOES<=0 THEN PRINT"ALL PHOTON TORPEDOES EXPENDED.":GOTO 1990 : REM Main Loop
4730 IF DEVICE(PHOTONTORPEDO)<0 THEN PRINT"PHOTON TUBES ARE NOT OPERATIONAL.":GOTO 1990 : REM Main Loop
4760 INPUT"PHOTON TORPEDO COURSE (1-9)";C1
4761 IF C1=9 THEN C1=1
4780 IF C1>=1 AND C1<9 THEN GOTO 4850
4790 PRINT"ENSIGN CHEKOV:  'INCORRECT COURSE DATA, SIR!'"
4800 GOTO 1990 : REM Main Loop
4850 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1))
4851 ENERGY=ENERGY-2
4852 TORPEDOES=TORPEDOES-1
4860 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1))
4861 X=SECTX
4862 Y=SECTY
4910 PRINT"TORPEDO TRACK:"
4920 X=X+X1
4921 Y=Y+X2
4922 X3=INT(X+.5)
4923 Y3=INT(Y+.5)
4960 IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN GOTO 5490
5000 PRINT"               ";X3;",";Y3
5001 A$="   "
5002 Z1=X
5003 Z2=Y
5004 GOSUB 8830 : REM Check for object in map
5050 IF Z3<>0 THEN GOTO 4920
5060 A$="+K+"
5061 Z1=X
5062 Z2=Y
5063 GOSUB 8830 : REM Check for object in map
5064 IF Z3=0 THEN GOTO 5210
5110 PRINT
5111 PRINT "*** KLINGON DESTROYED ***"
5112 PRINT
5113 KLINGONCNT=KLINGONCNT-1
5114 TOTALKLINGONS=TOTALKLINGONS-1
5115 IF TOTALKLINGONS<=0 THEN GOTO 6370
5150 FOR I=1 TO 3
5151     IF X3=KLINGONS(I,KLINGX)AND Y3=KLINGONS(I,KLINGY)THEN GOTO 5190
5180 NEXT I
5181 I=3
5190 KLINGONS(I,KLINGSTR)=0
5191 GOTO 5430
5210 A$=" * "
5211 Z1=X
5212 Z2=Y
5213 GOSUB 8830 : REM Check for object in map
5214 IF Z3=0 THEN GOTO 5280
5260 PRINT"STAR AT";X3;",";Y3;"ABSORBED TORPEDO ENERGY."
5261 GOSUB 6000 : REM Klingon attack
5262 GOTO 1990 : REM Main Loop
5280 A$=">B<"
5281 Z1=X
5282 Z2=Y
5283 GOSUB 8830 : REM Check for object in map
5284 IF Z3=0 THEN GOTO 4760 REM Use ">B<" to match placed base symbol
5330 PRINT "*** STARBASE DESTROYED ***"
5331 BASECNT=BASECNT-1
5332 TOTALBASES=TOTALBASES-1
5360 IF TOTALBASES>0 OR TOTALKLINGONS>DATE-INITDATE-TIMELIMIT THEN GOTO 5400
5370 PRINT"THAT DOES IT, CAPTAIN!!  YOU ARE HEREBY RELIEVED OF COMMAND"
5380 PRINT"AND SENTENCED TO 99 STARDATES AT HARD LABOR ON CYGNUS 12!!"
5390 GOTO 6270
5400 PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD TO CONSIDER"
5410 PRINT"COURT MARTIAL!"
5411 ISDOCKED=0
5430 Z1=X
5431 Z2=Y
5432 A$="   "
5433 GOSUB 8670 : REM Place object in map
5470 GALAXY(QUADX,QUADY)=KLINGONCNT*100+BASECNT*10+STARCNT
5471 SEEN(QUADX,QUADY)=GALAXY(QUADX,QUADY)
5472 GOSUB 6000 : REM Klingon attack
5473 GOTO 1990 : REM Main Loop
5490 PRINT"TORPEDO MISSED."
5491 PRINT
5492 GOSUB 6000 : REM Klingon attack
5493 GOTO 1990 : REM Main Loop

5527 REM ----------------------------------------
5528 REM  SHE - SHIELDS CONTROL
5529 REM ----------------------------------------

5530 IF DEVICE(SHIELDS)<0 THEN PRINT"SHIELD CONTROL INOPERABLE.":GOTO 1990 : REM Main Loop
5560 PRINT"ENERGY AVAILABLE =";ENERGY+SHIELD;
5561 INPUT"NUMBER OF UNITS TO SHIELDS";X
5580 IF X<0 OR SHIELD=X THEN PRINT"<SHIELDS UNCHANGED>":GOTO 1990 : REM Main Loop
5590 IF X<=ENERGY+SHIELD THEN GOTO 5630
5600 PRINT"SHIELD CONTROL:  'THIS IS NOT THE FEDERATION TREASURY.'" 
5610 PRINT"<SHIELDS UNCHANGED>"
5611 GOTO 1990 : REM Main Loop
5630 ENERGY=ENERGY+SHIELD-X
5631 SHIELD=X
5632 PRINT"DEFLECTOR CONTROL ROOM:"
5660 PRINT"  'SHIELDS NOW AT";INT(SHIELD);"UNITS PER YOUR COMMAND.'"
5661 GOTO 1990 : REM Main Loop

5687 REM ----------------------------------------
5688 REM  DAM - DAMAGE CONTROL REPORT
5689 REM ----------------------------------------

5690 IF DEVICE(DAMAGECONTROL)>=0 THEN GOTO 5910
5700 PRINT"DAMAGE CONTROL REPORT NOT AVAILABLE."
5701 IF ISDOCKED=0 THEN GOTO 1990 : REM Main Loop
5720 D3=0
5721 FOR I=1 TO 8
5722     IF DEVICE(I)<0 THEN D3=D3+.1
5760 NEXT I
5761 IF D3=0 THEN GOTO 1990 : REM Main Loop
5780 PRINT
5781 D3=D3+REPAIRFACTOR
5782 IF D3>=1 THEN D3=.9
5810 PRINT"TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP;"
5820 PRINT"ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);"STARDATES."
5840 INPUT"WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)";A$
5860 IF A$<>"Y"THEN GOTO 1990 : REM Main Loop
5870 FOR I=1 TO 8
5871     IF DEVICE(I)<0 THEN DEVICE(I)=0
5890 NEXT I
5891 DATE=DATE+D3+.1
5910 PRINT
5911 PRINT"DEVICE             STATE OF REPAIR"
5912 PRINT"------             ---------------"
5913 FOR I=1 TO 8
5920     R1=I  ' Set R1 for device name lookup
5921     GOSUB 8790 : REM Get device name
5922     PRINT G2$;LEFT$(Z$,25-LEN(G2$));INT(DEVICE(I)*100)*.01
5950 NEXT I
5951 PRINT
5952 IF ISDOCKED<>0 THEN GOTO 5720
5980 GOTO 1990 : REM Main Loop

5999 REM Klingon attack routine
6000 IF KLINGONCNT<=0 THEN RETURN
6010 IF ISDOCKED<>0 THEN PRINT"STARBASE SHIELDS PROTECT THE ENTERPRISE.":RETURN
6040 FOR I=1 TO 3
6041     IF KLINGONS(I,KLINGSTR)<=0 THEN GOTO 6200
6060     H=INT((KLINGONS(I,KLINGSTR)/FND(1))*(2+RND(1)))
6061     SHIELD=SHIELD-H
6062     KLINGONS(I,KLINGSTR)=KLINGONS(I,KLINGSTR)/(3+RND(0))
6080     PRINT
6081     PRINT H;"UNIT HIT ON ENTERPRISE FROM SECTOR";KLINGONS(I,KLINGX);",";KLINGONS(I,KLINGY);"."
6090     IF SHIELD<=0 THEN GOTO 6240
6100     PRINT"      <SHIELDS DOWN TO";SHIELD;"UNITS>"
6101     IF H<20 THEN GOTO 6200
6120     IF RND(1)>.6 OR H/SHIELD<=.02 THEN GOTO 6200
6140     R1=INT(RND(1)*8+1)
6141     DEVICE(R1)=DEVICE(R1)-H/SHIELD-.5*RND(1)
6142     GOSUB 8790 : REM Get device name
6170     PRINT"DAMAGE CONTROL: '";G2$;" DAMAGED BY THE HIT'"
6200 NEXT I
6201 RETURN
6220 PRINT
6221 PRINT"IT IS STARDATE";DATE;"."
6222 PRINT
6223 GOTO 6270

6240 PRINT
6241 PRINT"THE ENTERPRISE HAS BEEN DESTROYED.  THE FEDERATION ";
6250 PRINT"WILL BE CONQUERED."
6251 GOTO 6220

6267 REM ----------------------------------------
6268 REM  XXX - RESIGN COMMAND
6269 REM ----------------------------------------

6270 PRINT"THERE WERE";TOTALKLINGONS;"KLINGON BATTLE CRUISERS LEFT AT"
6280 PRINT"THE END OF YOUR MISSION."
6290 PRINT
6291 PRINT
6292 IF TOTALBASES=0 THEN GOTO 6360
6310 REM
6320 REM
6330 REM
6331 REM
6360 END
6370 PRINT"CONGRATULATIONS, CAPTAIN!  THE LAST KLINGON BATTLE CRUISER"
6380 PRINT"MENACING THE FEDERATION HAS BEEN DESTROYED."
6381 PRINT
6400 PRINT"YOUR EFFICIENCY RATING IS";1000*(INITKLINGONS/(DATE-INITDATE))^2
6401 GOTO 6290

6429 REM Check for nearby starbases and update status
6430 FOR I=SECTX-1 TO SECTX+1
6431     FOR J=SECTY-1 TO SECTY+1
6450         IF INT(I+.5)<1 OR INT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN GOTO 6540
6490         A$=">B<"
6491         Z1=I
6492 Z2=J
6493         GOSUB 8830 : REM Check for object in map
6494         IF Z3=1 THEN GOTO 6580
6540     NEXT J
6541 NEXT I
6542 ISDOCKED=0
6543 GOTO 6650
6580 ISDOCKED=1
6581 C$="DOCKED"
6582 ENERGY=INITENERGY
6583 TORPEDOES=INITTORP
6620 PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES."
6621 SHIELD=0
6622 GOTO 6720
6650 IF KLINGONCNT>0 THEN C$="*RED*":GOTO 6720
6660 C$="GREEN"
6661 IF ENERGY<INITENERGY*.1 THEN C$="YELLOW"
6720 IF DEVICE(SRSSENSOR)>=0 THEN GOTO 6770
6730 PRINT
6731 PRINT"*** SHORT RANGE SENSORS ARE OUT ***"
6732 PRINT
6733 RETURN
6770 O1$="   +--1---2---3---4---5---6---7---8-+"
6771 PRINT O1$
6772 FOR I=1 TO 8
6773 PRINT I;"|";
6820 FOR J=(I-1)*24+1 TO(I-1)*24+22 STEP 3
6821     PRINT" ";MID$(Q$,J,3);
6822 NEXT J
6823 PRINT"|";I;
6830 IF I=1 THEN GOTO 6850
6831 IF I=2 THEN GOTO 6900
6832 IF I=3 THEN GOTO 6960
6833 IF I=4 THEN GOTO 7020
6834 IF I=5 THEN GOTO 7070
6835 IF I=6 THEN GOTO 7120
6836 IF I=7 THEN GOTO 7180
6837 IF I=8 THEN GOTO 7240
6850 PRINT"        STARDATE           ";INT(DATE*10)*.1
6852 GOTO 7260
6900 PRINT"        CONDITION          ";C$
6902 GOTO 7260
6960 PRINT"        QUADRANT           ";QUADX;",";QUADY
6961 GOTO 7260
7020 PRINT"        SECTOR             ";SECTX;",";SECTY
7021 GOTO 7260
7070 PRINT"        PHOTON TORPEDOES   ";INT(TORPEDOES)
7072 GOTO 7260
7120 PRINT"        TOTAL ENERGY       ";INT(ENERGY+SHIELD)
7122 GOTO 7260
7180 PRINT"        SHIELDS            ";INT(SHIELD)
7182 GOTO 7260
7240 PRINT"        KLINGONS REMAINING";INT(TOTALKLINGONS)
7260 NEXT I
7261 PRINT O1$
7262 RETURN

7287 REM ----------------------------------------
7288 REM  COM - LIBRARY-COMPUTER
7289 REM ----------------------------------------

7290 IF DEVICE(COMPUTER)<0 THEN PRINT"COMPUTER DISABLED.":GOTO 1990 : REM Main Loop
7320 INPUT"COMPUTER ACTIVE AND AWAITING COMMAND";A
7321 IF A<0 THEN GOTO 1990 : REM Main Loop
7350 PRINT
7351 H8=1
7352 IF A+1=1 THEN GOTO 7540
7353 IF A+1=2 THEN GOTO 7900
7354 IF A+1=3 THEN GOTO 8070
7355 IF A+1=4 THEN GOTO 8500
7356 IF A+1=5 THEN GOTO 8150
7357 IF A+1=6 THEN GOTO 7400
7360 PRINT"FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
7365 PRINT "-----------------------------------------"
7366 PRINT
7370 PRINT"   0 = CUMULATIVE GALTIC RECORD"
7372 PRINT"   1 = STATUS REPORT"
7374 PRINT"   2 = PHOTON TORPEDO DATA"
7376 PRINT"   3 = STARBASE NAV DATA"
7378 PRINT"   4 = DIRECTION/DISTANCE CALCULATOR"
7380 PRINT"   5 = GALAXY 'REGION NAME' MAP"
7381 PRINT
7382 GOTO 7320

7397 REM ----------------------------------------
7398 REM  5 - GALAXY REGION NAME MAP
7399 REM ----------------------------------------

7400 H8=0
7401 BASENAMEONLY=1
7402 PRINT"                        THE GALAXY"

7537 REM '----------------------------------------
7538 REM ' 0 - CUMULATIVE GALACTIC RECORD
7539 REM '----------------------------------------

7540 REM
7542 REM
7543 PRINT
7544 PRINT"       ";
7545 PRINT"COMPUTER RECORD OF GALAXY FOR QUADRANT";QUADX;",";QUADY
7546 PRINT
7550 PRINT"       1     2     3     4     5     6     7     8"
7560 O1$="    +-----+-----+-----+-----+-----+-----+-----+-----+"
7570 PRINT O1$
7571 FOR I=1 TO 8
7572     PRINT I;" ";
7573     IF H8=0 THEN GOTO 7740
7630     FOR J=1 TO 8
7631         PRINT"| ";
7632         IF SEEN(I,J)=0 THEN PRINT"*** ";:GOTO 7720
7700         PRINT RIGHT$(STR$(SEEN(I,J)+1000),3);" ";
7720         IF J=8 THEN PRINT "|"
7721     NEXT J
7722     GOTO 7850
7740     Z4=I
7741     Z5=1
7742     GOSUB 9030 : REM Get region name
7743     J0=INT(15-.5*LEN(G2$))
7744     PRINT TAB(J0);G2$;
7800     Z5=5
7801     GOSUB 9030 : REM Get region name
7802     J0=INT(39-.5*LEN(G2$))
7803     PRINT TAB(J0);G2$
7850     PRINT O1$
7851 NEXT I
7852 PRINT
7853 GOTO 1990 : REM Main Loop

7897 REM ----------------------------------------
7898 REM  1 - STATUS REPORT
7899 REM ----------------------------------------

7900 PRINT "   STATUS REPORT:"
7901 PRINT "   -------------"
7902 X$=""
7903 IF TOTALKLINGONS>1 THEN X$="S"
7940 PRINT TOTALKLINGONS;"KLINGON";X$;" LEFT."
7960 PRINT" MISSION MUST BE COMPLETED IN";.1*INT((INITDATE+TIMELIMIT-DATE)*10);"STARDATES."
7970 X$="S"
7971 IF TOTALBASES<2 THEN X$="":IF TOTALBASES<1 THEN GOTO 8010
7980 PRINT" THE FEDERATION IS MAINTAINING";TOTALBASES;"STARBASE";X$;" IN THE GALAXY."
7990 GOTO 5690
8010 PRINT"YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN IN"
8020 PRINT"  THE GALAXY -- YOU HAVE NO STARBASES LEFT!"
8021 GOTO 5690

8067 REM ----------------------------------------
8068 REM  2 - PHOTON TORPEDO DATA
8069 REM ----------------------------------------

8070 IF KLINGONCNT<=0 THEN GOTO 4270
8080 X$=""
8081 IF KLINGONCNT>1 THEN X$="S"
8090 PRINT"FROM ENTERPRISE TO KLINGON BATTLE CRUSER";X$
8100 H8=0
8101 FOR I=1 TO 3
8102     IF KLINGONS(I,KLINGSTR)<=0 THEN GOTO 8480
8110     W1=KLINGONS(I,KLINGX)
8111     X=KLINGONS(I,KLINGY)
8120     C1=SECTX
8121     A=SECTY
8122     GOTO 8220

8147 REM ----------------------------------------
8148 REM  4 - DIRECTION/DISTANCE CALCULATOR
8149 REM ----------------------------------------

8150     PRINT"DIRECTION/DISTANCE CALCULATOR:"
8160     PRINT"YOU ARE AT QUADRANT ";QUADX;",";QUADY;" SECTOR ";SECTX;",";SECTY;"."
8170     INPUT"PLEASE ENTER INITIAL COORDINATES (X,Y)";C1,A
8200     INPUT"FINAL COORDINATES (X,Y)";W1,X
8220     X=X-A
8221     A=C1-W1
8222     IF X<0 THEN GOTO 8350
8250     IF A<0 THEN GOTO 8410
8260     IF X>0 THEN GOTO 8280
8270     IF A=0 THEN C1=5:GOTO 8290
8280     C1=1
8290     IF ABS(A)<=ABS(X)THEN GOTO 8330
8310     PRINT"DIRECTION =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A))
8311     GOTO 8460
8330     PRINT"DIRECTION =";C1+(ABS(A)/ABS(X))
8331     GOTO 8460
8350     IF A>0 THEN C1=3:GOTO 8420
8360     IF X<>0 THEN C1=5:GOTO 8290
8410     C1=7
8420     IF ABS(A)>=ABS(X)THEN GOTO 8450
8430     PRINT"DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X))
8431     GOTO 8460
8450     PRINT"DIRECTION =";C1+(ABS(X)/ABS(A))
8460     PRINT"DISTANCE =";SQR(X^2+A^2)
8461     IF H8=1 THEN GOTO 1990 : REM Main Loop
8480 NEXT I
8481 GOTO 1990 : REM Main Loop

8497 REM ----------------------------------------
8498 REM  3 - STARBASE NAV DATA
8499 REM ----------------------------------------

8500 IF BASECNT<>0 THEN PRINT"FROM ENTERPRISE TO STARBASE:":W1=BASEX:X=BASEY:GOTO 8120
8510 PRINT"MR. SPOCK:  'SENSORS SHOW NO STARBASES IN THIS QUADRANT.'";
8520 GOTO 1990 : REM Main Loop

8589 REM Find random empty sector coordinates
8590 R1=INT(RND(1)*8+1)
8591 R2=INT(RND(1)*8+1)
8592 A$="   "
8593 Z1=R1
8594 Z2=R2
8595 GOSUB 8830 : REM Check for object in map
8596 IF Z3=0 THEN GOTO 8590
8600 RETURN
8669 REM Place object on map at coordinates
8670 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
8675 IF LEN(A$)<>3 THEN PRINT"ERROR":STOP
8680 IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
8690 IF S8=190 THEN Q$=LEFT$(Q$,189)+A$:RETURN
8700 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8)
8701 RETURN

8789 REM Get device name by index in R1
8790 IF R1=1 THEN GOTO 8800
8791 IF R1=2 THEN GOTO 8802
8792 IF R1=3 THEN GOTO 8804
8793 IF R1=4 THEN GOTO 8806
8794 IF R1=5 THEN GOTO 8808
8795 IF R1=6 THEN GOTO 8810
8796 IF R1=7 THEN GOTO 8812
8797 IF R1=8 THEN GOTO 8814
8800 G2$="WARP ENGINES"
8801 RETURN
8802 G2$="SHORT RANGE SENSORS"
8803 RETURN
8804 G2$="LONG RANGE SENSORS"
8805 RETURN
8806 G2$="PHASER CONTROL"
8807 RETURN
8808 G2$="PHOTON TUBES"
8809 RETURN
8810 G2$="DAMAGE CONTROL"
8811 RETURN
8812 G2$="SHIELD CONTROL"
8813 RETURN
8814 G2$="LIBRARY-COMPUTER"
8815 RETURN

8829 REM Check for object at specific coordinates
8830 Z1=INT(Z1+.5)
8831 Z2=INT(Z2+.5)
8832 S8=(Z2-1)*3+(Z1-1)*24+1
8833 Z3=0
8890 IF MID$(Q$,S8,3)<>A$ THEN RETURN
8900 Z3=1
8901 RETURN

9029 REM Get star system name based on coordinates
9030 IF Z5>4 THEN GOTO 9120
9031 IF Z4=1 THEN GOTO 9040
9032 IF Z4=2 THEN GOTO 9050
9033 IF Z4=3 THEN GOTO 9060
9034 IF Z4=4 THEN GOTO 9070
9035 IF Z4=5 THEN GOTO 9080
9036 IF Z4=6 THEN GOTO 9090
9037 IF Z4=7 THEN GOTO 9100
9038 IF Z4=8 THEN GOTO 9110
9040 G2$="ANTARES"
9041 GOTO 9210
9050 G2$="RIGEL"
9051 GOTO 9210
9060 G2$="PROCYON"
9061 GOTO 9210
9070 G2$="VEGA"
9071 GOTO 9210
9080 G2$="CANOPUS"
9081 GOTO 9210
9090 G2$="ALTAIR"
9091 GOTO 9210
9100 G2$="SAGITTARIUS"
9101 GOTO 9210
9110 G2$="POLLUX"
9111 GOTO 9210
9120 IF Z4=1 THEN GOTO 9130
9121 IF Z4=2 THEN GOTO 9140
9122 IF Z4=3 THEN GOTO 9150
9123 IF Z4=4 THEN GOTO 9160
9124 IF Z4=5 THEN GOTO 9170
9125 IF Z4=6 THEN GOTO 9180
9126 IF Z4=7 THEN GOTO 9190
9127 IF Z4=8 THEN GOTO 9200
9130 G2$="SIRIUS"
9131 GOTO 9210
9140 G2$="DENEB"
9141 GOTO 9210
9150 G2$="CAPELLA"
9151 GOTO 9210
9160 G2$="BETELGEUSE"
9161 GOTO 9210
9170 G2$="ALDEBARAN"
9171 GOTO 9210
9180 G2$="REGULUS"
9181 GOTO 9210
9190 G2$="ARCTURUS"
9191 GOTO 9210
9200 G2$="SPICA"
9210 IF BASENAMEONLY=1 THEN RETURN
9211 IF Z5=1 THEN GOTO 9230
9212 IF Z5=2 THEN GOTO 9240
9213 IF Z5=3 THEN GOTO 9250
9214 IF Z5=4 THEN GOTO 9260
9215 IF Z5=5 THEN GOTO 9230
9216 IF Z5=6 THEN GOTO 9240
9217 IF Z5=7 THEN GOTO 9250
9218 IF Z5=8 THEN GOTO 9260
9220 RETURN
9230 G2$=G2$+" I"
9231 RETURN
9240 G2$=G2$+" II"
9241 RETURN
9250 G2$=G2$+" III"
9251 RETURN
9260 G2$=G2$+" IV"
9261 RETURN
9999 END
